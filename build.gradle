plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

group 'run.tere.bot'
version '1.1.4'

repositories {
    mavenCentral()
    maven {
        url 'https://m2.dv8tion.net/releases'
    }
}

dependencies {
    implementation 'com.google.code.gson:gson:2.9.1'
    implementation group: 'net.dv8tion', name: 'JDA', version:'5.1.2'
    implementation group: 'com.sedmelluq', name: 'lavaplayer', version: '1.3.77'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

test {
    useJUnitPlatform()
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.register('generateVersionProperties') {
    def outputDir = "$buildDir/generated/resources"
    outputs.dir outputDir

    doLast {
        file("$outputDir/version.properties").text = "version=${project.version}"
    }
}

processResources.dependsOn generateVersionProperties
processResources {
    from("$buildDir/generated/resources") {
        into '.'
    }
}

jar {
    manifest {
        attributes 'Main-Class': "run.tere.bot.Main"
    }
}